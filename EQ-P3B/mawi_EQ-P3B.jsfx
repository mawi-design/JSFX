desc:mawi EQ-P3B
/*
version: 1.06
changelog: 
  * hidpi/retina support
tags: EQ, Filter
author: mawi
copyright (C) 2023 and later mawi
license: GNU AGPLv3 - https://www.gnu.org/licenses/agpl-3.0.html
provides: 
  img/*
  mawi_EQ-P3B_Manual.pdf
about:
  # EQ-P3B
  ---
  ## Vintage passive EQ

  The EQ-P3B is modeled on a legendary passive equalizer developed in the 1950s. It is one of the most appreciated and iconic equalizers in audio production and has had a great influence on the sound design of many music productions. Some functions have been changed to the EQ-P3B to make it even more universally applicable. 

  The highlight at a glance:
  * analog hardware as model
  * digital filters without distortion (decramped filters)
  * simultaneous frequency boosts and cuts
  * vintage flair with rich and harmonically pleasing sound
  * low CPU, 0 latency, optimal for recording
  * no noise, ideal for mixing and mastering
  * attractive GUI, freely scalable
  * fully automatable with sample accurate automation
  * automatic bypass when no signal or playback stops
  * open source code, i.e. modifiable
  * freeware, no annoying activation
  * JSFX is optimally integrated in REAPER

  If you want to **support** my work or buy me a coffee, you can send my a donation via Paypal: mawi01@mail.de
*/

in_pin:EQ-P3B Input 1
in_pin:EQ-P3B Input 2

out_pin:EQ-P3B Output 1
out_pin:EQ-P3B Output 2

slider1:slider_eq_bypass=1<0,1,1{Out,In}>-EQ Out/In
//slider2:
slider3:slider_low_shelf_boost=0<0,10,0.01>-Lo Boost
slider4:slider_low_shelf_atten=0<0,10,0.01>-Lo Atten
slider5:slider_low_shelf_frequency=1<0,3,1{20 Hz,30 Hz,60 Hz,100 Hz}>-Lo CPS
//slider6:
slider7:slider_high_peak_boost=0<0,10,0.01>-Hi Boost
slider8:slider_high_peak_bandwidth=5<0,10,0.01>-Hi Bandwidth
slider9:slider_high_peak_frequency=0<0,6,1{3 kHz,4 kHz,5 kHz,8 kHz,10 kHz,12 kHz,16 kHz}>-Hi KCS Boost
//slider10:
slider11:slider_high_shelf_atten=0<0,10,0.01>-Hi Atten
slider12:slider_high_shelf_frequency=2<0,2,1{5 kHz,10 kHz,20 kHz}>-Hi KCS Atten
//slider13:
slider14:slider_eq_power=1<0,1,1{Off,On}>-Power
//slider15:
slider16:slider_trim=0<-18,18,0.01>-Trim (dB)

options:no_meter

@serialize
file_var(0, _global.mawi_eq_p3b_show_tooltip);
file_var(0, sample_accurate_automation);
file_var(0, _global.mawi_eq_p3b_auto_bypass_on_stop);

@init
ext_tail_size = -1;
gfx_ext_retina = 1;

gfx_loadimg(0, "./img/background.png");
gfx_loadimg(1, "./img/button_eq_in.png");
gfx_loadimg(2, "./img/knob_low_boost.png");
gfx_loadimg(3, "./img/knob_low_atten.png");
gfx_loadimg(4, "./img/knob_low_cps.png");
gfx_loadimg(5, "./img/knob_high_boost_kcs.png");
gfx_loadimg(6, "./img/knob_high_boost.png");
gfx_loadimg(7, "./img/knob_high_boost_bandwidth.png");
gfx_loadimg(8, "./img/knob_high_atten.png");
gfx_loadimg(9, "./img/knob_high_atten_kcs.png");
gfx_loadimg(10, "./img/lamp_eq_on.png");
gfx_loadimg(11, "./img/knob_eq_on.png");
gfx_loadimg(12, "./img/menu.png");
gfx_loadimg(13, "./img/knob_trim.png");

degrees = $pi/180;

function background_color_(red, green, blue)
instance()
local()
global(gfx_clear)
(
  gfx_clear = red + green * 256 + blue * 65536;
);

function background_img_(img_file_id, rotation)
instance(img_width, img_height)
local()
global(gfx_x, gfx_y, gfx_w, gui_scale_factor, degrees)
(
  gfx_getimgdim(img_file_id, img_width, img_height);
  gui_scale_factor = gfx_w / img_width;
  
  gfx_x = gfx_y = 0;
  gfx_blit(img_file_id, gui_scale_factor, rotation * degrees);
);

function resize_icon_(icon_size)
instance()
local()
global(gfx_ext_retina, gfx_ext_flags, gfx_w, gfx_h, gui_scale_factor)
(
  gfx_ext_flags == 0 ? (
    gfx_set(1, 1, 1, 0.5);
    icon_size *= gfx_ext_retina;
    gfx_triangle(gfx_w, gfx_h, gfx_w, gfx_h - icon_size, gfx_w - icon_size, gfx_h);
  );
);

// peak
function cosh_(x) 
instance()
local()
global()
(
  (exp(x) + exp(-x)) / 2;
);

function peak_coefficients_(slider_frequency, slider_gain, slider_q)
instance(G, omega, omega_0, q, phi_0, phi_1, phi_2, a1, a2, A_0, A_1, A_2, R_1, R_2, B_0, B_2, B_1, W, b0, b1, b2)
local()
global(srate)
(
  G = 10^(slider_gain / 20);
  omega = 2 * $pi * slider_frequency / srate;
  omega_0 = omega;
  q = 1 / (sqrt(G) * slider_q);
  
  phi_0 = 1 - sin(omega / 2)^2;
  phi_1 = sin(omega / 2)^2;
  phi_2 = 4 * phi_0 * phi_1;
  
  a1 = q <= 1 ?
       -2 * exp(-q * omega_0) * cos(sqrt(1 - sqr(q)) * omega_0) :
       -2 * exp(-q * omega_0) * cosh_(sqrt(sqr(q) - 1) * omega_0);
  a2 = exp(-2 * q * omega_0);
  
  A_0 = sqr(1 + a1 + a2);
  A_1 = sqr(1 - a1 + a2);
  A_2 = -4 * a2;
  
  R_1 = ( A_0 * phi_0 + A_1 * phi_1 + A_2 * phi_2) * sqr(G);
  R_2 = (-A_0 + A_1 + 4 * (phi_0 - phi_1) * A_2) * sqr(G);
  
  B_0 = A_0;
  B_2 = (R_1 - R_2 * phi_1 - B_0) / (4 * sqr(phi_1));
  B_1 = R_2 + B_0 + 4 * (phi_1 - phi_0) * B_2;
  
  W = 0.5 * (sqrt(B_0) + sqrt(B_1));
  
  b0 = 0.5 * (W + sqrt(sqr(W) + B_2));
  b1 = 0.5 * (sqrt(B_0) - sqrt(B_1));
  b2 = -B_2 / (4 * b0);
);

function peak_processing_(input)
instance(a1, a2, b0, b1, b2, v_0, v_1, v_2, w_0, w_1, w_2, output)
local()
global()
(
  v_0 = input;
  w_0 = - this..a1 * w_1 - this..a2 * w_2 + this..b0 * v_0 + this..b1 * v_1 + this..b2 * v_2;
  
  output = w_0;
  
  v_2 = v_1;
  w_2 = w_1;
  v_1 = v_0;
  w_1 = w_0;
);

// lowpass
function lpf_coefficients_(slider_frequency)
instance(a)
local(omega_0, cos_omega_0)
global(srate)
(
  omega_0 = 2 * $pi * slider_frequency / srate;
  cos_omega_0 = cos(omega_0) - 2;
  a = sqrt(sqr(cos_omega_0) - 1) + cos_omega_0 + 1;
);

function lpf_processing_(input)
instance(output, a)
(
  output += this..a * (input - output);
);

// highpass
function hpf_coefficients_(slider_frequency)
instance(g, G_HP)
global(srate)
(
  g = tan($pi * slider_frequency / srate);
  G_HP = 1 / (1 + g);
);

function hpf_processing_(input)
instance(output, x_1, g, G_HP)
global()
(
  output = this..G_HP * (input - x_1);
  x_1 = x_1 + 2 * this..g * output;
  
  output;
);

function dB_conversion_(value)
instance(dB)
local()
global()
(
  dB = 10^(value / 20);
);

function slider_parameter_linear_(index, value, default, minimum, maximum, step)
instance(slider_index, slider_value, slider_default, slider_type,
slider_minimum, slider_maximum, slider_step)
local()
global()
(
  slider_index = index;
  slider_value = value;
  slider_default = default;
  slider_minimum = minimum;
  slider_maximum = maximum;
  slider_step = step;
  slider_type = "lin";
);

function stepped_value_()
instance(slider_value, slider_step)
local()
global()
(
  slider_step === 0 ? slider_value : floor(slider_value / slider_step) * slider_step;
);

function slider_lin_scaled_to_normalized_()
instance(slider_value, slider_minimum, slider_maximum, slider_normalized)
local()
global()
(
  slider_value = min(max(slider_value, slider_minimum), slider_maximum);
  slider_normalized = (slider_value - slider_minimum) / (slider_maximum - slider_minimum);
  slider_normalized = min(max(slider_normalized, 0), 1);
);

function slider_normalized_to_lin_scaled_()
instance(slider_normalized, slider_value, slider_minimum, slider_maximum, slider_power)
local()
global()
(
  slider_normalized = min(max(slider_normalized, 0), 1);
  slider_value = slider_normalized * (slider_maximum - slider_minimum) + slider_minimum;
  slider_value = min(max(slider_value, slider_minimum), slider_maximum);
);

function image_(file_id, scaling, rotation, pos_x, pos_y, frames)
instance(slider_value, slider_minimum, slider_maximum, img_scaling,
img_rotation, img_pos_x, img_pos_y, img_width, img_height, slider_normalized)
local(img_file_id, img_frames, img_frame, img_frame_pos_x, img_frame_pos_y)
global(gfx_x, gfx_y, gfx_a, gui_scale_factor, degrees)
(
  // image parameter
  img_file_id = file_id;
  img_scaling = scaling;
  img_rotation = rotation;
  img_pos_x = pos_x;
  img_pos_y = pos_y;
  img_frames = frames;
  
  // calculate image frame
  gfx_getimgdim(img_file_id, img_width, img_height);
  img_height /= img_frames;
  
  img_frame = floor((img_frames - 1) * slider_normalized);
  img_frame_pos_x = 0;
  img_frame_pos_y = img_height * img_frame;
  
  // draw image frame
  gfx_x = img_pos_x * gui_scale_factor;
  gfx_y = img_pos_y * gui_scale_factor;
  //gfx_a = 0.5; //
  gfx_blit(img_file_id, img_scaling * gui_scale_factor, img_rotation * 
  degrees, img_frame_pos_x, img_frame_pos_y, img_width, img_height);
);

function mouse_logic_()
instance(img_scaling, img_pos_x, img_pos_y, img_width, img_height,
mouse_over, mouse_drag, mouse_scroll, mouse_click_left, mouse_click_right,
mouse_click_left_count, mouse_click_right_count,  
mouse_click_left_time, mouse_click_right_time,
mouse_doubleclicktime, mouse_click_delta,
mouse_y_previous, mouse_wheel_previous, mouse_cap_previous)
local()
global(mouse_cap, mouse_x, mouse_y, mouse_wheel, gui_scale_factor)
(
  // mouse parameter
  mouse_doubleclicktime = 0.25;
  
  // left mouse button
  mouse_click_left = (mouse_cap&1 && !(mouse_cap_previous&1)) ? (
    mouse_click_delta = time_precise() - mouse_click_left_time;
    mouse_click_left = 1;
    mouse_click_left_time = time_precise() - 0.001;
    mouse_click_delta < mouse_doubleclicktime ? (
      mouse_click_left_count += 1;
    ):(
      mouse_click_left_count = 1;
    );
  );
  
  // right mouse button
  mouse_click_right = (mouse_cap&2 && !(mouse_cap_previous&2)) ? (
    mouse_click_delta = time_precise() - mouse_click_right_time;
    mouse_click_right = 1;
    mouse_click_right_time = time_precise() - 0.001;
    mouse_click_delta < mouse_doubleclicktime ? (
      mouse_click_right_count += 1;
    ):(
      mouse_click_right_count = 1;
    );
  );
  
  mouse_cap_previous = mouse_cap;
  
  // collision / mouse over
  mouse_x >= img_pos_x * gui_scale_factor &&
  mouse_x <= img_pos_x * gui_scale_factor + img_width * img_scaling * gui_scale_factor &&
  mouse_y >= img_pos_y * gui_scale_factor &&
  mouse_y <= img_pos_y * gui_scale_factor + img_height * img_scaling * gui_scale_factor ? (
    !mouse_cap ? mouse_over = 1;
     mouse_cap ? mouse_click_left_count >= 1;
     mouse_over && mouse_click_left && mouse_click_left_count ? mouse_drag = 1;
     mouse_wheel != mouse_wheel_previous ? mouse_scroll = 1;
   ):(
     mouse_over = mouse_click_left_count = mouse_click_left = mouse_scroll = 0;
   );
  
  !mouse_cap ? mouse_drag = 0;
  !mouse_wheel ? mouse_scroll = 0;
);

function button_logic_()
instance(slider_index, slider_value, slider_default, slider_normalized,
slider_minimum, slider_maximum, touch_state,
mouse_over, mouse_drag, mouse_click_left)
local()
global(current_cursor)
(
  // mouse parameter
  mouse_over || mouse_drag ? current_cursor = 32649;
  
  // process
  mouse_click_left ? slider_value = !slider_value;
  slider_normalized = slider_value;
  
  // slider automation
  mouse_click_left ? (
    slider_automate(slider(slider_index), 0);
    touch_state = 1;
  ) : !mouse_drag && touch_state == 1 ? (
    slider_automate(slider(slider_index), 1);
    touch_state = 0;
  );
  
  slider_value;
);

function knob_logic_()
instance(slider_index, slider_value, slider_default,
slider_minimum, slider_maximum, slider_value_previous, 
slider_normalized, slider_type, touch_state, 
mouse_over, mouse_drag, mouse_scroll, mouse_click_left,
mouse_y_previous, mouse_wheel_previous, knob_resolution)
local()
global(gfx_ext_retina, mouse_cap, mouse_wheel, mouse_y, current_cursor)
(
  // knob parameter
  knob_resolution = 1 / 250 / gfx_ext_retina;
  mouse_cap&4 ? knob_resolution *= 0.1;
  mouse_click_left >= 2 ? slider_value = slider_default;
  mouse_over || mouse_drag ? current_cursor = 32645;
  
  // process
  slider_type == "lin" ? this.slider_lin_scaled_to_normalized_();
  
  mouse_drag ? slider_normalized += (mouse_y_previous - mouse_y) * knob_resolution;
  mouse_scroll ? slider_normalized -= (mouse_wheel_previous - mouse_wheel) / 1.2 * 0.1 * knob_resolution;
  
  mouse_y_previous = mouse_y;
  mouse_wheel_previous = mouse_wheel;
  
  slider_type == "lin" ? this.slider_normalized_to_lin_scaled_();
  
  // slider automation
  mouse_drag || mouse_scroll ? (
    slider_automate(slider(slider_index), 0);
    touch_state = 1;
  ) : !mouse_drag || !mouse_scroll && touch_state == 1 ? (
    slider_automate(slider(slider_index), 1);
    touch_state = 0;
  );
  
  slider_value;
);

function menu_()
instance(mouse_over, mouse_drag, mouse_click_left, mouse_cap_previous, mouse_click_right, menu, #menustr)
local()
global(gfx_x, gfx_y,  mouse_x, mouse_y, current_cursor, _global.mawi_eq_p3b_show_tooltip, sample_accurate_automation,
_global.mawi_eq_p3b_auto_bypass_on_stop)
(
  // menu parameter
  mouse_over || mouse_drag ? current_cursor = 32649;
  
  // process
  mouse_click_left ? (
    gfx_x = mouse_x;
    gfx_y = mouse_y;
    
    menu = gfx_showmenu(#menustr);
    
    menu == 1 ? _global.mawi_eq_p3b_show_tooltip = !_global.mawi_eq_p3b_show_tooltip;
    menu == 2 ? sample_accurate_automation = !sample_accurate_automation;
    menu == 3 ? _global.mawi_eq_p3b_auto_bypass_on_stop = !_global.mawi_eq_p3b_auto_bypass_on_stop;
    
    // undo point
    menu >= 1 ? sliderchange(-1);
  );
  
  sprintf(#menustr,"%sShow tooltip|%sSample accurate automation (more CPU)|%sAuto bypass on stop (save CPU)|",
          _global.mawi_eq_p3b_show_tooltip == 0 ? "!" : "", 
          sample_accurate_automation == 1 ? "!" : "",
          _global.mawi_eq_p3b_auto_bypass_on_stop == 1 ? "!" : "");
);

function update_sliders_() (
  // slider midpoint offset
  midpoint = (7.5 - 0) / (10 - 0);
  base = ((midpoint - 1) / midpoint)^2;
  
  // low shelf boost
  low_shelf_boost.slider_log_scaled = (10 - 0) * ((base^(slider_low_shelf_boost * 0.1) -1) / (base - 1)) + 0;
  low_shelf_boost_value = low_shelf_boost.slider_log_scaled * 0.1 * (17 - -48) + -48;
  low_shelf_boost_value = min(max(low_shelf_boost_value, -48), 17);
  low_shelf_boost_inv = (10 - low_shelf_boost.slider_log_scaled) + 0.5;
  low_shelf_boost_inv = min(max(low_shelf_boost_inv, 0.5), 10);
  slider_low_shelf_frequency == 0 ? low_shelf_frequency = 20 * 1.2;
  slider_low_shelf_frequency == 1 ? low_shelf_frequency = 30 * 1.5;
  slider_low_shelf_frequency == 2 ? low_shelf_frequency = 60 * 1.5;
  slider_low_shelf_frequency == 3 ? low_shelf_frequency = 100 * 1.5;
  low_shelf_boost_frequency.lpf_coefficients_(low_shelf_frequency * low_shelf_boost_inv);
  low_shelf_boost.dB_conversion_(low_shelf_boost_value);
  
  // low shelf atten
  low_shelf_atten.slider_log_scaled = (10 - 0) * ((base^(slider_low_shelf_atten * 0.1) -1) / (base - 1)) + 0;
  low_shelf_atten_value = low_shelf_atten.slider_log_scaled * 0.1 * (-0.9 - -36) + -36;
  low_shelf_atten_value = min(max(low_shelf_atten_value, -36), -0.9);
  low_shelf_atten_inv = (10.6 - low_shelf_atten.slider_log_scaled) + 0.5;
  low_shelf_atten_inv = min(max(low_shelf_atten_inv, 0.5), 10.6);
  low_shelf_atten_frequency.lpf_coefficients_(low_shelf_frequency * 10 * low_shelf_atten_inv);
  low_shelf_atten.dB_conversion_(low_shelf_atten_value);
  
  // high peak boost
  //// slider midpoint offset
  midpoint_high_peak_boost = (2 - 0) / (10 - 0);
  base_high_peak_boost = ((midpoint_high_peak_boost - 1) / midpoint_high_peak_boost)^2;
  high_peak_boost.slider_log_scaled = (10 - 0) * ((base_high_peak_boost^(slider_high_peak_boost * 0.1) -1)/(base_high_peak_boost - 1)) + 0;
  high_peak_boost_value = high_peak_boost.slider_log_scaled * 0.1 * (18 - 0) + 0;
  high_peak_boost_value = min(max(high_peak_boost_value, 0), 18);
  // 3 kcs
  slider_high_peak_frequency == 0 ? (
    high_peak_boost_frequency = 3200;
    high_peak_bandwidth_minimum = 0.5;
    high_peak_bandwidth_maximum = 2.5;
  );
  // 4 kcs
  slider_high_peak_frequency == 1 ? (
    high_peak_boost_frequency = 4300;
    high_peak_bandwidth_minimum = 0.1;
    high_peak_bandwidth_maximum = 2.0;
  );
  // 5 kcs
  slider_high_peak_frequency == 2 ? (
    high_peak_boost_frequency = 5300;
    high_peak_bandwidth_minimum = 0.9;
    high_peak_bandwidth_maximum = 2.9;
  );
  // 8 kcs
  slider_high_peak_frequency == 3 ? (
    high_peak_boost_frequency = 8250;
    high_peak_bandwidth_minimum = 0.5;
    high_peak_bandwidth_maximum = 2.9;
  );
  // 10 kcs
  slider_high_peak_frequency == 4 ? (
    high_peak_boost_frequency = 10250;
    high_peak_bandwidth_minimum = 0.01;
    high_peak_bandwidth_maximum = 2.5;
  );
  // 12 kcs
  slider_high_peak_frequency == 5 ? (
    high_peak_boost_frequency = 12300;
    high_peak_bandwidth_minimum = 0.6;
    high_peak_bandwidth_maximum = 2.3;
  );
  // 16 kcs
  slider_high_peak_frequency == 6 ? (
    high_peak_boost_frequency = 16000;
    high_peak_bandwidth_minimum = 1.4;
    high_peak_bandwidth_maximum = 2.0;
  );
  high_peak_bandwidth = (slider_high_peak_bandwidth * 0.1 - high_peak_bandwidth_maximum) / (high_peak_bandwidth_minimum - high_peak_bandwidth_maximum);
  high_peak_boost_frequency.peak_coefficients_(high_peak_boost_frequency, high_peak_boost_value, high_peak_bandwidth);
  
  // high shelf atten
  high_shelf_atten.slider_log_scaled = (10 - 0) * ((base^(slider_high_shelf_atten * 0.1) -1) / (base -1)) + 0;
  high_shelf_atten_value = high_shelf_atten.slider_log_scaled * 0.1 * (-0.9 - -48) + -48;
  high_shelf_atten_value = min(max(high_shelf_atten_value, -48), -0.9);
  slider_high_shelf_frequency == 0 ? high_shelf_frequency = 5000 * 0.135;
  slider_high_shelf_frequency == 1 ? high_shelf_frequency = 10000 * 0.135;
  slider_high_shelf_frequency == 2 ? high_shelf_frequency = 20000 * 0.135;
  high_shelf_atten_frequency.hpf_coefficients_(high_shelf_frequency);
  high_shelf_atten.dB_conversion_(high_shelf_atten_value);
  
  // output
  trim.dB_conversion_(slider_trim);
);

function automation_block_()
instance (slider_index, slider_value, number_of_samples_into_block, slider_increment, change_sample_position,
target_slider_value)
local()
global()
(
  number_of_samples_into_block = 0;
  slider_increment = 0;
  change_sample_position = slider_next_chg(slider_index, target_slider_value);
  change_sample_position != -1 ? (
    slider_increment = (target_slider_value - slider_value) / change_sample_position;
  );
);

function automation_sample_()
instance (slider_index, slider_value, number_of_samples_into_block, change_sample_position, slider_increment, target_slider_value)
global()
(
  number_of_samples_into_block == change_sample_position ? (
    change_sample_position = slider_next_chg(slider_index, target_slider_value);
    change_sample_position != number_of_samples_into_block ? (
      slider_increment = (target_slider_value - slider_value) / (change_sample_position - number_of_samples_into_block);
    );
  );
  
  change_sample_position != -1 ? (
    slider(slider_index) += slider_increment;
    slider_value = slider(slider_index);
    update_sliders_();
    
    number_of_samples_into_block += 1;
  );
);

function tooltip_(pos_x, pos_y, alignment, text)
instance(mouse_over, mouse_drag, mouse_scroll, 
mouse_click_left_count, 
tooltip_time_hover, tooltip_timer, 
tooltip_time_delta, tooltip, 
tooltip_fade_alpha, slider_value)
local(tooltip_pos_x, tooltip_pos_y, tooltip_alignment, 
tooltip_text, textbox_width, textbox_height, 
value_width, value_height, 
padding_width, padding_height,
border_width, border_height)
global(gfx_x, gfx_y, gfx_r, gfx_g, gfx_b, gfx_a, 
gui_scale_factor, #tooltip, time_current)
(
  tooltip_pos_x = pos_x;
  tooltip_pos_y = pos_y;
  tooltip_alignment = alignment;
  tooltip_text = text;
  
  // tooltip
  mouse_over || mouse_drag || mouse_scroll ? (
    tooltip_timer += tooltip_time_delta;
    tooltip_timer >= 0.5 || mouse_drag || mouse_click_left_count ? (
      tooltip = 1;
      gfx_setfont(1, "Verdana", 30 * gui_scale_factor);
      slider_value < 1000 ? (
        sprintf(#tooltip, tooltip_text, slider_value);
      ):(
        sprintf(#tooltip, tooltip_text, slider_value / 1000);
      );
      gfx_measurestr(#tooltip, value_width *= gui_scale_factor, value_height *= gui_scale_factor);
      // textbox
      tooltip_pos_x *= gui_scale_factor;
      tooltip_pos_y *= gui_scale_factor;
      padding_width = 20 * gui_scale_factor;
      padding_height = 10 * gui_scale_factor;
      border_width = 4 * gui_scale_factor;
      border_height = 4 * gui_scale_factor;
      textbox_width = value_width + padding_width;
      textbox_height = value_height + padding_height;
      // left-aligned
      tooltip_alignment == 0 ? (
        // border
        gfx_x = tooltip_pos_x - border_width;
        gfx_y = tooltip_pos_y - border_height - textbox_height / 2;
        gfx_set(0, 0, 0, tooltip_fade_alpha - 0.6);
        gfx_rect(gfx_x, gfx_y, textbox_width + 2 * border_width, textbox_height + 2 * border_height);
        // box
        gfx_x = tooltip_pos_x;
        gfx_y = tooltip_pos_y - textbox_height / 2;
        gfx_set(0.9, 0.9, 0.9, tooltip_fade_alpha);
        gfx_rect(gfx_x, gfx_y, textbox_width, textbox_height);
        // text
        gfx_x = tooltip_pos_x + padding_width / 2;
        gfx_y = tooltip_pos_y;
        gfx_set(0.1, 0.1, 0.1, tooltip_fade_alpha);
        gfx_drawstr(#tooltip, 0+4+256, gfx_x, gfx_y);
      );
      // centered
      tooltip_alignment == 1 ? (
        // border
        gfx_x = tooltip_pos_x - border_width - textbox_width / 2;
        gfx_y = tooltip_pos_y - border_height - textbox_height / 2;
        gfx_set(0, 0, 0, tooltip_fade_alpha - 0.6);
        gfx_rect(gfx_x, gfx_y, textbox_width + 2 * border_width, textbox_height + 2 * border_height);
        // box
        gfx_x = tooltip_pos_x - textbox_width / 2;
        gfx_y = tooltip_pos_y - textbox_height / 2;
        gfx_set(0.9, 0.9, 0.9, tooltip_fade_alpha);
        gfx_rect(gfx_x, gfx_y, textbox_width, textbox_height);
        // text
        gfx_x = tooltip_pos_x;
        gfx_y = tooltip_pos_y;
        gfx_set(0.1, 0.1, 0.1, tooltip_fade_alpha);
        gfx_drawstr(#tooltip, 1+4+256, gfx_x, gfx_y);
      );
      // right-aligned
      tooltip_alignment == 2 ? (
        // border
        gfx_x = tooltip_pos_x - border_width - textbox_width;
        gfx_y = tooltip_pos_y - border_height - textbox_height / 2;
        gfx_set(0, 0, 0, tooltip_fade_alpha - 0.6);
        gfx_rect(gfx_x, gfx_y, textbox_width + 2 * border_width, textbox_height + 2 * border_height);
        // box
        gfx_x = tooltip_pos_x - textbox_width;
        gfx_y = tooltip_pos_y - textbox_height / 2;
        gfx_set(0.9, 0.9, 0.9, tooltip_fade_alpha);
        gfx_rect(gfx_x, gfx_y, textbox_width, textbox_height);
        // text
        gfx_x = tooltip_pos_x - padding_width / 2;
        gfx_y = tooltip_pos_y;
        gfx_set(0.1, 0.1, 0.1, tooltip_fade_alpha);
        gfx_drawstr(#tooltip, 2+4+256, gfx_x, gfx_y);
      );
    );
  );
  
  time_current = time_precise();
  tooltip_time_delta = time_current - tooltip_time_hover;
  tooltip_time_hover = time_current;
  
  !mouse_over && !mouse_drag ? tooltip_timer = tooltip = 0;
  
  tooltip > 0 ? (
    tooltip_fade_alpha < 1 ? tooltip_fade_alpha += 0.2;
    tooltip_fade_alpha = min(tooltip_fade_alpha, 1);
  ):(
    tooltip_fade_alpha > 0 ? (
      tooltip_fade_alpha -= 0.2;
    ):(
      tooltip_fade_alpha = tooltip = 0;
    );
  );

);

// slider parameter
// bypass
eq_bypass.slider_parameter_linear_(1, slider_eq_bypass, 1, 0, 1, 1);
// low shelf
low_shelf_boost.slider_parameter_linear_(3, slider_low_shelf_boost, 0, 0, 10, 0.01);
low_shelf_atten.slider_parameter_linear_(4, slider_low_shelf_atten, 0, 0, 10, 0.01);
low_shelf_frequency.slider_parameter_linear_(5, slider_low_shelf_frequency, 1, 0, 3, 1);
// high peak
high_peak_boost.slider_parameter_linear_(7, slider_high_peak_boost, 0, 0, 10, 0.01);
high_peak_bandwidth.slider_parameter_linear_(8, slider_high_peak_bandwidth, 5, 0, 10, 0.01);
high_peak_frequency.slider_parameter_linear_(9, slider_high_peak_frequency, 0, 0, 6, 1);
// high shelf
high_shelf_atten.slider_parameter_linear_(11, slider_high_shelf_atten, 0, 0, 10, 0.01);
high_shelf_frequency.slider_parameter_linear_(12, slider_high_shelf_frequency, 2, 0, 2, 1);
// power
eq_power.slider_parameter_linear_(14, slider_eq_power, 1, 0, 1, 1);
// trim
trim.slider_parameter_linear_(16, slider_trim, 0, -18, 18, 0.01);

// DC blocker
input_dc_blocker.hpf_coefficients_(5);
output_dc_blocker.hpf_coefficients_(5);

@gfx 960 182
background_color_(25, 26, 27);
background_img_(0, 0);

current_cursor = 32512;

// options
options.mouse_logic_();
options.menu_();
options.image_(12, 0.05, 0, 40, 60, 1);
// bypass
eq_bypass.mouse_logic_();
eq_bypass.button_logic_();
eq_bypass.image_(1, 1, 0, 172, 187, 2);
slider_eq_bypass = eq_bypass.slider_value;
// low shelf boost
low_shelf_boost.mouse_logic_();
low_shelf_boost.knob_logic_();
low_shelf_boost.image_(2, 1, 0, 243, 37, 151);
slider_low_shelf_boost = low_shelf_boost.stepped_value_();
// low shelf atten
low_shelf_atten.mouse_logic_();
low_shelf_atten.knob_logic_();
low_shelf_atten.image_(3, 1, 0, 460, 37, 151);
slider_low_shelf_atten = low_shelf_atten.stepped_value_();
// low shelf frequency
low_shelf_frequency.mouse_logic_();
low_shelf_frequency.knob_logic_();
low_shelf_frequency.image_(4, 1, 0, 663, 132, 4);
slider_low_shelf_frequency = low_shelf_frequency.stepped_value_();
// high peak frequency
high_peak_frequency.mouse_logic_();
high_peak_frequency.knob_logic_();
high_peak_frequency.image_(5, 1, 0, 860, 130, 7);
slider_high_peak_frequency = high_peak_frequency.stepped_value_();
// high peak boost
high_peak_boost.mouse_logic_();
high_peak_boost.knob_logic_();
high_peak_boost.image_(6, 1, 0, 1066, 39, 151);
slider_high_peak_boost = high_peak_boost.stepped_value_();
// high peak bandwidth
high_peak_bandwidth.mouse_logic_();
high_peak_bandwidth.knob_logic_();
high_peak_bandwidth.image_(7, 1, 0, 1227, 211, 151);
slider_high_peak_bandwidth = high_peak_bandwidth.stepped_value_();
// high shelf atten
high_shelf_atten.mouse_logic_();
high_shelf_atten.knob_logic_();
high_shelf_atten.image_(8, 1, 0, 1312, 38, 151);
slider_high_shelf_atten = high_shelf_atten.stepped_value_();
// high shelf frequency
high_shelf_frequency.mouse_logic_();
high_shelf_frequency.knob_logic_();
high_shelf_frequency.image_(9, 1, 0, 1501, 144, 3);
slider_high_shelf_frequency = high_shelf_frequency.stepped_value_();
// power
eq_power.mouse_logic_();
eq_power.button_logic_();
eq_power.image_(10, 1, 0, 1645, 39, 2);
eq_power.image_(11, 1, 0, 1667, 182, 2);
slider_eq_power = eq_power.slider_value;
// trim
trim.mouse_logic_();
trim.knob_logic_();
trim.image_(13, 1, 0, 256, 249, 151);
slider_trim = trim.stepped_value_();

update_sliders_();

// tooltp parameter
_global.mawi_eq_p3b_show_tooltip == 0 ? (
  // bypass
  eq_bypass.tooltip_(212, 150, 1, slider_eq_bypass ? "EQ: In" : "EQ: Out");
  // low shelf
  low_shelf_boost.tooltip_(343, 255, 1, "Boost: %.2f");
  low_shelf_atten.tooltip_(555, 255, 1, "Atten: %.2f");
  low_shelf_frequency.tooltip_(752, 75, 1, 
  slider_low_shelf_frequency == 0 ? "CPS: 20" : 
  slider_low_shelf_frequency == 1 ? "CPS: 30" : 
  slider_low_shelf_frequency == 2 ? "CPS: 60" : "CPS: 100");
  // high peak
  high_peak_boost.tooltip_(1166, 255, 1, "Boost: %.2f");
  high_peak_bandwidth.tooltip_(1290, 190, 1, "Bandwidth: %.2f");
  high_peak_frequency.tooltip_(965, 75, 1, 
  slider_high_peak_frequency == 0 ? "KCS: 3": 
  slider_high_peak_frequency == 1 ? "KCS: 4" : 
  slider_high_peak_frequency == 2 ? "KCS: 5" : 
  slider_high_peak_frequency == 3 ? "KCS: 8" : 
  slider_high_peak_frequency == 4 ? "KCS: 10" : 
  slider_high_peak_frequency == 5 ? "KCS: 12" : "KCS: 16");
  // high shelf
  high_shelf_atten.tooltip_(1411, 255, 1, "Atten: %.2f");
  high_shelf_frequency.tooltip_(1570, 95, 1, 
  slider_high_shelf_frequency == 0 ? "KCS: 5" : 
  slider_high_shelf_frequency == 1 ? "KCS: 10" : "KCS: 20");
  // power
  eq_power.tooltip_(1725, 160, 1, slider_eq_power ? "Power: On" : "Power: Off");
  // trim
  trim.tooltip_(300, 225, 1, "Trim: %.2f dB");
);

resize_icon_(15);

gfx_setcursor(current_cursor);

@slider
// bypass
eq_bypass.slider_value = slider_eq_bypass;
// low shelf
low_shelf_boost.slider_value = slider_low_shelf_boost;
low_shelf_atten.slider_value = slider_low_shelf_atten;
low_shelf_frequency.slider_value = slider_low_shelf_frequency;
// high peak
high_peak_boost.slider_value = slider_high_peak_boost;
high_peak_bandwidth.slider_value = slider_high_peak_bandwidth;
high_peak_frequency.slider_value = slider_high_peak_frequency;
// high shelf
high_shelf_atten.slider_value = slider_high_shelf_atten;
high_shelf_frequency.slider_value = slider_high_shelf_frequency;
// power
eq_power.slider_value = slider_eq_power;
// trim
trim.slider_value = slider_trim;

update_sliders_();

@block
sample_accurate_automation == 1 ? (
  // bypass
  eq_bypass.automation_block_();
  // low shelf
  low_shelf_boost.automation_block_();
  low_shelf_atten.automation_block_();
  low_shelf_frequency.automation_block_();
  // high peak
  high_peak_boost.automation_block_();
  high_peak_bandwidth.automation_block_();
  high_peak_frequency.automation_block_();
  // high shelf
  high_shelf_atten.automation_block_();
  high_shelf_frequency.automation_block_();
  // power
  eq_power.automation_block_();
  // trim
  trim.automation_block_();
);

@sample
play_state >= _global.mawi_eq_p3b_auto_bypass_on_stop ? (
  sample_accurate_automation == 1 ? (
    // bypass
    eq_bypass.automation_sample_();
    // low shelf
    low_shelf_boost.automation_sample_();
    low_shelf_atten.automation_sample_();
    low_shelf_frequency.automation_sample_();
    // high peak
    high_peak_boost.automation_sample_();
    high_peak_bandwidth.automation_sample_();
    high_peak_frequency.automation_sample_();
    // high shelf
    high_shelf_atten.automation_sample_();
    high_shelf_frequency.automation_sample_();
    // power
    eq_power.automation_sample_();
    // trim
    trim.automation_sample_();
  );
  
  slider_eq_power == 1 ? (
    // input gain
    input_1 = input_dc_blocker.1.hpf_processing_(spl0);
    input_2 = input_dc_blocker.2.hpf_processing_(spl1);
    
    // EQ
    slider_eq_bypass == 1 ? (
      // EQ output
      eq_output_1 = input_1;
      eq_output_2 = input_2;
      // low shelf boost
      slider_low_shelf_boost != 0 ? (
        low_shelf_boost_output_1 = low_shelf_boost_frequency.1.lpf_processing_(input_1);
        low_shelf_boost_output_2 = low_shelf_boost_frequency.2.lpf_processing_(input_2);
        low_shelf_boost_output_1 *= low_shelf_boost.dB;
        low_shelf_boost_output_2 *= low_shelf_boost.dB;
        eq_output_1 += low_shelf_boost_output_1;
        eq_output_2 += low_shelf_boost_output_2;
      );
      // low shelf atten
      slider_low_shelf_atten != 0 ? (
        low_shelf_atten_output_1 = low_shelf_atten_frequency.1.lpf_processing_(input_1);
        low_shelf_atten_output_2 = low_shelf_atten_frequency.2.lpf_processing_(input_2);
        low_shelf_atten_output_1 *= low_shelf_atten.dB;
        low_shelf_atten_output_2 *= low_shelf_atten.dB;
        eq_output_1 -= low_shelf_atten_output_1;
        eq_output_2 -= low_shelf_atten_output_2;
      );
      // high peak boost
      slider_high_peak_boost != 0 ? (
        eq_output_1 = high_peak_boost_frequency.1.peak_processing_(eq_output_1);
        eq_output_2 = high_peak_boost_frequency.2.peak_processing_(eq_output_2);
      );
      // high shelf atten
      slider_high_shelf_atten != 0 ? (
        high_shelf_atten_output_1 = high_shelf_atten_frequency.1.hpf_processing_(eq_output_1);
        high_shelf_atten_output_2 = high_shelf_atten_frequency.2.hpf_processing_(eq_output_2);
        high_shelf_atten_output_1 *= high_shelf_atten.dB;
        high_shelf_atten_output_2 *= high_shelf_atten.dB;
        eq_output_1 -= high_shelf_atten_output_1;
        eq_output_2 -= high_shelf_atten_output_2;
      );
      // trim
      eq_output_1 *= trim.dB;
      eq_output_2 *= trim.dB;
    ):(
      // bypassed EQ output
      eq_output_1 = input_1;
      eq_output_2 = input_2;
    );
    
    // harmonics
    eq_output_1 > 0 ? (
      harmonics_output_1 = eq_output_1;
    ):(
      harmonics_output_1 = eq_output_1 / (1 + abs(eq_output_1));
    );
    
    eq_output_2 > 0 ? (
      harmonics_output_2 = eq_output_2;
    ):(
      harmonics_output_2 = eq_output_2 / (1 + abs(eq_output_2));
    );
    
    // output
    output_1 = 0.96 * eq_output_1 + 0.04 * harmonics_output_1;
    output_2 = 0.96 * eq_output_2 + 0.04 * harmonics_output_2;
    
    spl0 = output_dc_blocker.1.hpf_processing_(output_1);
    spl1 = output_dc_blocker.2.hpf_processing_(output_2);
  );
);

